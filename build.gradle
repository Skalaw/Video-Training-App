Properties videoApi = new Properties()
videoApi.load(new FileInputStream("./core/src/main/resources/video_api.properties"))
Properties keystore = new Properties()
keystore.load(new FileInputStream("./secure/keystore.properties"))

ext {
    compileSdkVersion = 19
    configuration = [
            applicationId: "com.rocksolidapps.movies",
            buildTools   : "24.0.2",
            compileSdk   : 23,
            minSdk       : 17,
            targetSdk    : 23,
            versionCode  : getCodeVersion(),
            versionName  : "0.0.1-b"
    ]

    library = [
            androidSupport: "24.2.0",
            retrofit      : "2.1.0",
            picasso       : "2.5.2",
            picassoOkHttp3: "1.0.2",
            dagger        : "1.2.5",
            leakcanary    : "1.4-beta2",
            butterKnife   : "8.2.1",
            rxjava        : "1.1.9",
            rxandroid     : "1.2.1",
            stetho        : "1.3.1"
    ]

    testing = [
            junit: "4.12"
    ]
}

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:2.1.3'
        classpath 'me.tatarka:gradle-retrolambda:3.2.5'
        classpath 'com.neenbedankt.gradle.plugins:android-apt:1.8'
    }
}

allprojects {
    repositories {
        jcenter()
    }
    project.ext {
        THE_MOVIE_DB_API_KEY = "\"${videoApi.apiKey}\"";
        THE_MOVIE_DB_USERNAME = "\"${videoApi.username}\"";
        THE_MOVIE_DB_PASSWORD = "\"${videoApi.password}\"";

        KEYSTORE_PATH = "${keystore.release_path}";
        KEYSTORE_PASSWORD = "${keystore.password}";
        KEYSTORE_ALIAS = "${keystore.alias}";
        KEYSTORE_ALIAS_PASSWORD = "${keystore.alias_password}";
    }
}

def int getCodeVersion() {
    def commitCount = 'git rev-list --count origin/develop HEAD'.execute().text.trim() as int
    return commitCount
}